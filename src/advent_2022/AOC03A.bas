 10 T0=TIME
 20 F$="AOC03I"
 30 ASSIGN# 1 TO F$
 40 DIM X$[60]
 50 DIM S(52)
 60 Z=0
 70 DEF FNP(Q$)
 80 Q=NUM(Q$)-96
 90 IF Q<0 THEN Q=Q+58
100 FNP=Q
110 FN END
120 ON ERROR GOTO 260
130 READ# 1 ; X$
140 OFF ERROR
150 FOR I=1 TO 52 @ S(I)=0 @ NEXT I
160 DISP X$
170 H=LEN(X$)/2
180 FOR I=1 TO H @ S(FNP(X$[I,I]))=1 @ NEXT I
190 FOR I=H+1 TO LEN(X$)
200 T=FNP(X$[I,I])
210 IF S(T)<>0 THEN P=T @ GOTO 230
220 NEXT I
230 DISP P
240 Z=Z+P
250 GOTO 120
260 ASSIGN# 1 TO *
270 DISP Z
280 DISP TIME-T0;"s"
290 END
